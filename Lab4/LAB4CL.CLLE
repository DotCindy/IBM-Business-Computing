             PGM
             DCLF       LAB4DSP
             DCL        &PRVSIGN *CHAR 13
             DCL        &RTNUSRPRF *CHAR 10
             DCL        &DATE *CHAR 13
             DCL        &TIME *CHAR 10
             DCL        &QACTJOB *DEC ( 5 0 )
             DCL        &QACTJOBCH *CHAR 5
             DCL        &MSGQLIB *CHAR 10
             DCL        &MSGQ *CHAR 10
             DCL        &MAX *CHAR 6

             /*** M A I N L I N E *****/
             SndRcvf
             DoWhile    (&In30 = '0')

                Select
                   When       (&Option = '1' *or &option = '01') CallSubr OPTION1
                   When       (&Option = '2' *or &option = '02') CallSubr OPTION2
                   When       (&Option = '3' *or &option = '03') CallSubr OPTION3
                   When       (&Option = '4' *or &option = '04') CallSubr OPTION4
                   When       (&Option = '5' *or &option = '05') CallSubr OPTION5
                   Otherwise  ChgVar &in30 '1'
                EndSelect

                SndRcvf
             Enddo

             /*** S U B R O U T I N E S *****/
             SUBR       OPTION1
                CALL       HWYTOLLRPG
             EndSubr

             SUBR       OPTION2
                RTVSYSVAL  SYSVAL(QMAXSIGN) RTNVAR(&MAX)
                CHGVAR     VAR(&MsgTxt1) VALUE('Maximum invalid signons allowed: ' *BCAT +
                             (&MAX) *BCAT '(tries)')
                CHGVAR     VAR(&MsgTxt2) VALUE('CLLE Programs can report on the system and +
                             manage system wide settings')
             ENDSUBR

             SUBR       OPTION3
                RTVUSRPRF  MSGQ(&MSGQ) MSGQLIB(&MSGQLIB)
                CHGVAR     VAR(&MSGTXT1) VALUE('Signed on users message queue: ' *BCAT +
                             &MSGQLIB *TCAT '/' *CAT &MSGQ)
             ENDSUBR

             SUBR       OPTION4
                RTVSYSVAL  SYSVAL(QACTJOB) RTNVAR(&QACTJOB)
                chgvar     &QACTJOBCH &QACTJOB

                ChgVar     VAR(&MsgTxt1) VALUE('Initial number of active jobs for which +
                             storage is allocated is: ' *BCAT &QACTJOBCH)
                CHGVAR     VAR(&MsgTxt2) VALUE('CLLE Programs can report on the system and +
                             manage system wide settings')
             ENDSUBR

             SUBR       OPTION5
                RTVUSRPRF  RTNUSRPRF(&RTNUSRPRF) PRVSIGN(&PRVSIGN)
                CHGVAR     &Date (%SST(&PRVSIGN 4 2) *CAT '/' *CAT %SST(&PRVSIGN 6 2) *CAT '/' +
                             *CAT %SST(&PRVSIGN 2 2))

                ChgVar     &Time (%SST(&PRVSIGN 8 2) *CAT ':' *CAT %SST(&PRVSIGN 10 2) *CAT +
                             ':' *CAT %SST(&PRVSIGN 12 2) )
                ChgVar     &MsgTxt1 ('Previous sign on for user ' *bcat &RtnUsrPrf *bcat ' was +
                             on ' *CAT &Date *BCAT &Time)

                ChgVar     VAR(&MSGTXT2) VALUE('The value of user profile ''PRVSIGN'' +
                             parameter: [' *CAT &PRVSIGN *CAT '].')
             ENDSUBR
             ENDPGM          
